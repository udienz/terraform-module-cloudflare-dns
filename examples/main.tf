variable "cloudflare_records" {
  type        = any
  description = "values for the DNS records to create"
}

variable "cloudflare_records_defaults" {
  type        = any
  description = "default values for the DNS records to create"
  default = {
    proxied  = false
    ttl      = 60
    comment  = "Generated by terraform module cloudflare-dns"
    priority = null
    tags     = []
    settings = {}
  }
}

locals {
  cloudflare_records = [
    for record in var.cloudflare_records : merge(
      record,
      {
        dns_records = [
          for dns_record in record.dns_records : merge(var.cloudflare_records_defaults, dns_record)
        ]
      }
    )
  ]
}
module "cloudflare_dns" {
  source      = "../"
  for_each    = { for cloudflare_record in local.cloudflare_records : cloudflare_record.zone_name => cloudflare_record }
  account_id  = each.value.account_id
  zone_name   = each.value.zone_name
  dns_records = each.value.dns_records
}

variable "cloudflare_token" {
  type        = string
  description = "Cloudflare API token with permissions to manage DNS records"
  default     = null
}